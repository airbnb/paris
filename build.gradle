apply plugin: "com.github.ben-manes.versions"

buildscript {
    ext {
        ANDROID_PLUGIN_VERSION = '3.3.0'
        BUTTERKNIFE_VERSION = '10.0.0'
        KOTLIN_VERSION = '1.3.50'
        VERSIONS_VERSION = '0.20.0'
    }

    repositories {
        google()
        jcenter()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
    }

    dependencies {
        classpath "com.android.tools.build:gradle:$ANDROID_PLUGIN_VERSION"
        classpath "com.jakewharton:butterknife-gradle-plugin:$BUTTERKNIFE_VERSION"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$KOTLIN_VERSION"
        classpath "com.github.ben-manes:gradle-versions-plugin:$VERSIONS_VERSION"
    }
}

allprojects {
    repositories {
        google()
        jcenter()
    }

    configurations.all {
        exclude group: 'org.jetbrains.kotlin', module: 'kotlin-stdlib-jre7'
    }
}

subprojects { project ->
    apply from: "$rootDir/blessedDeps.gradle"
    // TODO Same for maven upload script?
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

dependencyUpdates.resolutionStrategy {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm', 'preview'].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
            }
            if (rejected) {
                selection.reject('Release candidate')
            }
        }
    }
}
